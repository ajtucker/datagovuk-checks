PREFIX sch: <http://purl.org/net/schemarama#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  [] a sch:Warning;
    sch:message "Properties should define both an rdfs:range and an rdfs:domain";
    sch:implicated ?x.
    
} WHERE {
  {
	  {
	    ?x a rdf:Property.
	  }
	  UNION
	  {
	    ?x a owl:DatatypeProperty.
	  }
	  UNION
	  {
	    ?x a owl:ObjectProperty.
	  }	  
  }
  OPTIONAL {
    ?x rdfs:range ?range.
  }
  OPTIONAL {
    ?x rdfs:domain ?domain.
  }      
  FILTER ( !bound(?range) || !bound(?domain) )
}